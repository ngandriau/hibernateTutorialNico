/*
 * Copyright 2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.2'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.2.4'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.7.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply from: 'gradle/publishing.gradle'
apply from: 'gradle/code-coverage.gradle'
apply from: 'gradle/code-quality.gradle'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile  'org.hibernate:hibernate-entitymanager:4.3.5.Final'
//    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'ch.qos.logback:logback-classic:1.1.2'
//    runtime 'ch.qos.logback:logback-core:1.1.2'

    runtime 'com.h2database:h2:1.2.145'
    runtime 'mysql:mysql-connector-java:5.1.30'
    testCompile 'junit:junit:4.11'
}

task sourceJar(type: Jar) {
    group 'Build'
    description 'An archive of the source code'
    classifier 'sources'
    from sourceSets.main.allSource
}

jar.finalizedBy sourceJar

artifacts {
    sourceJar
}

gradle.taskGraph.whenReady {
    if (project.plugins.hasPlugin('groovy')) {
        sourceSets.main.groovy.srcDirs.each    { it.mkdirs() }
        sourceSets.test.groovy.srcDirs.each    { it.mkdirs() }
    } else {
        sourceSets.main.java.srcDirs.each      { it.mkdirs() }
        sourceSets.test.java.srcDirs.each      { it.mkdirs() }
    }
    sourceSets.main.resources.srcDirs.each { it.mkdirs() }
    sourceSets.test.resources.srcDirs.each { it.mkdirs() }
}
